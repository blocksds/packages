# SPDX-License-Identifier: CC0-1.0
#
# SPDX-FileContributor: Antonio Niño Díaz, 2024

pkgname=blocksds-ptexconv
groups=(blocksds-extra)
pkgver=1.5.2.3_1
pkgrel=1
epoch=
pkgdesc="Tool to convert graphics to formats used by the Nintendo DS"
arch=("x86_64" "aarch64")
source=("$pkgname::git+https://github.com/blocksds/ptexconv#tag=v$pkgver")
url="https://github.com/blocksds/sdk"
license=("BSD-2-Clause")
sha256sums=('SKIP')
depends=(
	runtime-gcc-libs
	runtime-musl
)
makedepends=(
	runtime-musl-dev
)
options=('!strip')

. "/opt/wonderful/lib/wf-pacman/runtime-env-vars.sh"

build() {
	cd "$pkgname"

	sed -i -e 's/ifdef _WIN32/if 0/' src/main.c

	make -j`nproc`
}

package() {
	cd "$pkgname"

	export INSTALLDIR="$pkgdir$WF_DESTDIR"/thirdparty/blocksds/external/ptexconv

	make -j`nproc` install

	wf_runtime_patchelf $INSTALLDIR/ptexconv
}
