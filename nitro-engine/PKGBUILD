# SPDX-License-Identifier: CC0-1.0
#
# SPDX-FileContributor: Adrian "asie" Siekierka, 2023

pkgname=thirdparty-blocksds-nitroengine
groups=(thirdparty-blocksds-extra)
pkgver=0.14.0
pkgrel=1
epoch=
pkgdesc="AntonioND's library providing functions designed to simplify the process of making a 3D game"
arch=(any)
source=("$pkgname::git+https://github.com/AntonioND/nitro-engine#tag=v$pkgver")
url="https://github.com/AntonioND/nitro-engine"
license=("MIT")
depends=(thirdparty-blocksds-toolchain)
sha256sums=('SKIP')
options=('!strip')

. "/wf/config/runtime-env-vars.sh"

build() {
	wf_use_toolchain gcc-arm-none-eabi arm-none-eabi

	cd "$pkgname"
	make
}

package() {
	cd "$pkgname"

	BLOCKSDSEXT="$pkgdir$WF_DESTDIR"/thirdparty/blocksds/external \
	make -j1 install
}
